-- Copyright (c) 2019 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Airline where

import DA.Assert
import DA.List
import DA.Optional
import DA.TextMap as TM

data Class = Coach | Business | First deriving (Eq, Show, Ord)

template FlightInvite
  with
    ticket : Ticket
  where
    signatory ticket.airline
    observer ticket.passenger

    choice Accept_Invite : (ContractId Ticket, ContractId Flight)
      with
        flightCid : ContractId Flight
      controller ticket.passenger
      do
        flight <- fetch flightCid
        ticket.flightNumber === flight.flightNumber
        ticket.airline === flight.airline

        flightCid <- exercise flightCid Onboard with
          passenger = ticket.passenger
        ticketCid <- create ticket

        return (ticketCid, flightCid)


template Ticket
  with
    ticketClass : Class
    ticketRef : Text
    flightNumber : Text
    airline : Party
    passenger : Party
    seatNumber : Optional Text
    seatChoice : Bool
  where
    signatory airline, passenger

    choice CheckIn : (ContractId Ticket, ContractId Flight)
      with
        flightCid : ContractId Flight
        seat : Text
      controller if seatChoice then passenger else airline
      do
        flight <- fetch flightCid
        None === seatNumber
        flight.flightNumber === flightNumber
        flight.airline === airline
        let seatClass = (TM.lookup seat flight.seatClasses)
        assert $ fromSome seatClass <= ticketClass
        newTicketCid <- create this with seatNumber = Some seat
        newFlightCid <- exercise flightCid AssignSeat with
          passenger; seat; ticketRef
        return (newTicketCid, newFlightCid)

template Flight
  with
    seatClasses : TextMap Class
    allocation : TextMap Text
    flightNumber : Text
    airline : Party
    invitedPassengers : [Party]
    passengers : [Party]
  where
    signatory airline, passengers
    observer invitedPassengers

    choice AssignSeat : ContractId Flight
      with
        passenger : Party
        ticketRef : Text
        seat : Text
      controller [passenger, airline]
      do
        None === TM.lookup seat allocation

        create this with
          allocation = insert seat ticketRef allocation

    choice Onboard : ContractId Flight
      with
        passenger : Party
      controller [airline, passenger]
      do
        assert (passenger `elem` invitedPassengers)
        create this with passengers = passenger :: passengers

    choice TakeOff : ()
      controller airline
      do
        length passengers === TM.size allocation
        return ()

dajet1 = scenario do
  da <- getParty "DA"
  passengers@[p1, p2, p3, p4, p5]
    <- mapA getParty ["P1", "P2", "P3", "P4", "P5"]
  let
    flightNumber = "DA Force One"
    classes = [Coach, Coach, Business, Business, First]
    seatClasses = TM.fromList
      [ ("1A", First)
      , ("1B", First)
      , ("2A", Business)
      , ("2B", Business)
      , ("3A", Coach)
      , ("3B", Coach) ]
    tickets = map
      (\(seq, passenger, ticketClass) ->
        Ticket with
          ticketClass
          ticketRef = show seq
          flightNumber
          airline = da
          passenger
          seatNumber = None
          seatChoice = ticketClass > Coach
      )
      (zip3 [1..5] passengers classes)

  debug tickets
{- output:
  [
    Ticket {
      ticketClass = Coach,
      ticketRef = "1",
      flightNumber = "DA Force One",
      airline = 'DA',
      passenger = 'P1',
      seatNumber = None,
      seatChoice = False},
    Ticket {ticketClass = Coach, ticketRef = "2", flightNumber = "DA Force One", airline = 'DA', passenger = 'P2', seatNumber = None, seatChoice = False},
    Ticket {ticketClass = Business, ticketRef = "3", flightNumber = "DA Force One", airline = 'DA', passenger = 'P3', seatNumber = None, seatChoice = True},
    Ticket {ticketClass = Business, ticketRef = "4", flightNumber = "DA Force One", airline = 'DA', passenger = 'P4', seatNumber = None, seatChoice = True},
    Ticket {ticketClass = First, ticketRef = "5", flightNumber = "DA Force One", airline = 'DA', passenger = 'P5', seatNumber = None, seatChoice = True}]
-}

  submit da do
    inviteCids <- mapA (\ticket -> create FlightInvite with ticket) tickets
    flightCid <- create Flight with
      seatClasses
      allocation = TM.empty
      flightNumber
      airline = da
      invitedPassengers = passengers
      passengers = []
    return (inviteCids, flightCid)

dajet2 = scenario do
  da <- getParty "DA"
  -- remove p5!!!
  passengers@[p1, p2, p3, p4]
    <- mapA getParty ["P1", "P2", "P3", "P4"]

  (inviteCids, flightCid) <- dajet1

  (ticketCids, flightCid) <- foldr
    (\(passenger, inviteCid) acc -> do
      (ticketCids, flightCid) <- acc
      submit passenger do
        (ticketCid, flightCid) <- exercise inviteCid Accept_Invite with flightCid
        return (ticketCid :: ticketCids, flightCid) )
    (return ([], flightCid))
    (zip passengers inviteCids)

  debug (zip passengers inviteCids)
{- output:
  [('P1',<contract-id>),('P2',<contract-id>),('P3',<contract-id>),('P4',<contract-id>),('P5',<contract-id>)]
-}

  return (inviteCids, ticketCids, flightCid)

dajet3 = scenario do
  da <- getParty "DA"
  passengers@[p1, p2, p3, p4, p5]
    <- mapA getParty ["P1", "P2", "P3", "P4", "P5"]

  (inviteCids, [t1,t2,t3,t4], flightCid) <- dajet2

  let inviteCid = last inviteCids

  (t5, flightCid) <- submit p5 do
    exercise inviteCid Accept_Invite with flightCid

  return ([t1,t2,t3,t4,t5], flightCid)

dajet4 = scenario do
  da <- getParty "DA"
  passengers@[p1, p2, p3, p4]
    <- mapA getParty ["P1", "P2", "P3", "P4"]

  (ticketCids, flightCid) <- dajet3

  submitMustFail da do exercise flightCid TakeOff
  submitMustFail p1 do
    exercise (ticketCids !! 0) CheckIn with
      flightCid
      seat = "1A"
  submitMustFail da do
    exercise (ticketCids !! 0) CheckIn with
      flightCid
      seat = "1A"
  submitMustFail p2 do
    exercise (ticketCids !! 2) CheckIn with
      flightCid
      seat = "1A"

  (_, flightCid) <- submit da do
    exercise (ticketCids !! 0) CheckIn with
      flightCid
      seat = "3A"

  submitMustFail da do
    exercise (ticketCids !! 1) CheckIn with
      flightCid
      seat = "3A"

  return (ticketCids, flightCid)

dajet5 = scenario do
  da <- getParty "DA"
  passengers@[p1, p2, p3, p4]
    <- mapA getParty ["P1", "P2", "P3", "P4"]

  (ticketCids, flightCid) <- dajet4

  (_, flightCid) <- submit da do
    exercise (ticketCids !! 1) CheckIn with
      flightCid
      seat = "3B"

  submitMustFail da do
    exercise (ticketCids !! 2) CheckIn with
      flightCid
      seat = "2A"

  (_, flightCid) <- submit p3 do
    exercise (ticketCids !! 2) CheckIn with
      flightCid
      seat = "2A"

  (_, flightCid) <- submit p4 do
    exercise (ticketCids !! 3) CheckIn with
      flightCid
      seat = "2B"

  return (ticketCids, flightCid)

dajet6 = scenario do
  da <- getParty "DA"
  passengers@[p1, p2, p3, p4, p5]
    <- mapA getParty ["P1", "P2", "P3", "P4", "P5"]

  (ticketCids, flightCid) <- dajet5

  (_, flightCid) <- submit p5 do
    exercise (ticketCids !! 4) CheckIn with
      flightCid
      seat = "1A"

  return flightCid


dajet = scenario do
  da <- getParty "DA"

  flightCid <- dajet6

  submit da do exercise flightCid TakeOff
